// Top-level build file with configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.3'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        mavenLocal()
    }
}

subprojects {
    if (name != 'ZipSigner') {
        apply plugin: 'java'

        java {
            sourceCompatibility = JavaVersion.VERSION_17
            targetCompatibility = JavaVersion.VERSION_17
        }

        if (!["zipsigner-cmdline", "convert-keystore"].contains(name)) {
            apply plugin: 'maven-publish'

            publishing {
                publications {
                    mavenJava(MavenPublication) {
                        groupId = 'kellinwood.android'
                        from components.java
                    }
                }
            }
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}