plugins {
    id 'application'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

// mainClassName is deprecated in Gradle 8 - use mainClass instead
application {
    mainClass = 'kellinwood.zipsigner.cmdline.Main'

    applicationDistribution.into('') {
        from "."
        include 'log4j.properties'
    }

    applicationDefaultJvmArgs += ["-Dlog4j.properties=\$APP_HOME/log4j.properties"]

    startScripts {
        doLast {
            unixScript.text = unixScript.text.replace('\\$APP_HOME', '$APP_HOME')
            windowsScript.text = windowsScript.text.replace('\\$APP_HOME', '%APP_HOME%')
        }
    }
}

version = project.property('zipsigner-cmdline.version')

dependencies {
    implementation project(':kellinwood-logging-lib')
    implementation project(':kellinwood-logging-log4j')
    implementation project(':zipio-lib')
    implementation project(':zipsigner-lib')
    implementation project(':zipsigner-lib-optional')
    implementation 'commons-cli:commons-cli:1.6.0'
    implementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
    implementation 'log4j:log4j:' + project.property('log4j.version')
    testImplementation 'junit:junit:4.13.2'
}
